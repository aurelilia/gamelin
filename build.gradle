/*
 * Developed as part of the Gamelin project.
 * This file was last modified at 3/14/21, 7:46 PM.
 * Copyright 2021, see git repository at git.angm.xyz for authors and other info.
 * This file is under the GPL3 license. See LICENSE in the root directory of this repository for details.
 */

import java.text.SimpleDateFormat

buildscript {
    ext.kotlinVersion = "1.4.10"

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "org.jetbrains.dokka"

    sourceCompatibility = 1.8
    [compileJava, compileTestJava]*.options*.encoding = "UTF-8"

    version = new SimpleDateFormat("yyyyMMdd").format(new Date())
    ext {
        appName = "gamelin"
        gdxVersion = "1.9.14"
        ktxVersion = "1.9.14-b1"
        assetsDir = new File("../assets")
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }

    dokka {
        outputFormat = "html"
        outputDirectory = "$buildDir/doc"

        externalDocumentationLink {
            url = new URL("https://libgdx.badlogicgames.com/ci/ashley/docs/")
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

        implementation "io.github.microutils:kotlin-logging:1.6.22"
        implementation "ch.qos.logback:logback-classic:1.2.3"
    }
}

project(":desktop") {
    sourceSets.main.java.srcDirs = [ "src/" ]
    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

project(":core") {
    sourceSets.main.java.srcDirs = [ "src/main/" ]
    sourceSets.test.java.srcDirs = [ "src/test/" ]

    test {
        useJUnitPlatform()
        workingDir = project.assetsDir
    }

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        api "io.github.libktx:ktx-assets:$ktxVersion"
        api "io.github.libktx:ktx-actors:$ktxVersion"
        api "io.github.libktx:ktx-collections:$ktxVersion"
        api "io.github.libktx:ktx-style:$ktxVersion"
        api "io.github.libktx:ktx-scene2d:$ktxVersion"
        api "io.github.libktx:ktx-vis-style:$ktxVersion"
        api "io.github.libktx:ktx-vis:$ktxVersion"
        api "com.kotcrab.vis:vis-ui:1.4.11"

        testImplementation 'io.kotest:kotest-runner-junit5:4.4.3'
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }
}
